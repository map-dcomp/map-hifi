import org.gradle.internal.os.OperatingSystem

// Plugins
plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow'
    id 'application'
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
}

def props = new Properties()
file("../MAP-code/src/P2Protelis/gradle.properties").withInputStream { props.load(it) }
file("../MAP-code/src/gradle.common.properties").withInputStream { props.load(it) }
file("../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map'
version = props.getProperty("map_hifi_version")

mainClassName = 'com.bbn.map.hifi.background.BackgroundTrafficDriver'


description = """Background Traffic"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    providedRuntime
    all {
        exclude group: 'ch.qos.logback'
    }    
}

dependencies {
    api project(':hifi-resmgr')

    api group: 'commons-cli', name: 'commons-cli', version: props.getProperty("commons_cli_version")
    
    api group: 'org.slf4j', name: 'slf4j-api', version: props.getProperty("slf4j_version")
    api group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: props.getProperty("log4j_version")
    api group: 'org.apache.logging.log4j', name: 'log4j-core', version: props.getProperty("log4j_version")
    
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: props.getProperty("jackson_version")
    api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: props.getProperty("jackson_version")
    
    api group: 'com.diffplug.durian', name: 'durian', version: props.getProperty("durian_version")

    api group: 'org.apache.commons', name: 'commons-csv', version: props.getProperty("commons_csv_version")
    
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

    api group: 'dnsjava', name: 'dnsjava', version: '2.1.8'

    testImplementation group: 'junit', name: 'junit', version: props.getProperty("junit_version")
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: props.getProperty("hamcrest_version")
}

checkstyle {
    config = resources.text.fromFile('style.xml', 'UTF-8')
    showViolations = true
    ignoreFailures = false
    toolVersion = "8.10"
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

shadowJar {
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
 }

gitProperties {
    gitPropertiesDir = "${buildDir}/resources/main/com/bbn/map/hifi/background"
}
