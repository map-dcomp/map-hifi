import org.gradle.internal.os.OperatingSystem

// Plugins
plugins {
    id 'com.github.johnrengelman.shadow'
    id 'application'
}

def props = new Properties()
file("../MAP-code/src/P2Protelis/gradle.properties").withInputStream { props.load(it) }
file("../MAP-code/src/gradle.common.properties").withInputStream { props.load(it) }
file("../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map'
version = props.getProperty("map_hifi_version")

mainClassName = 'com.bbn.map.hifi.setup.StageExperiment'


description = """Stage Experiments"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
    providedRuntime
    all*.exclude group: 'ch.qos.logback'
}

dependencies {
    implementation project(':ClientDriver')
    
    testImplementation group: 'junit', name: 'junit', version: props.getProperty("junit_version")
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: props.getProperty("hamcrest_version")
}

checkstyle {
    config = resources.text.fromFile('style.xml', 'UTF-8')
    showViolations = true
    ignoreFailures = false
    toolVersion = "8.10"
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

shadowJar {
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
    
    from "build/iftop"
}

task iftopTar(type: Tar) {
    destinationDir  file('build/iftop')
    compression = Compression.GZIP
    archiveName = 'iftop.tar.gz'

    into('iftop') {
        from "iftop"
    }
}
shadowJar.dependsOn(iftopTar)
jar.dependsOn(iftopTar)

jar {
    from "build/iftop"
}
