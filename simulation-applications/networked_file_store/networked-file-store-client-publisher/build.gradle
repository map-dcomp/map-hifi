import org.gradle.internal.os.OperatingSystem

buildscript {
  repositories {
      jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
  }
}

// Plugins
plugins {
    //id 'com.dorongold.task-tree' version '1.3'
    id 'com.github.johnrengelman.shadow'
    id 'application'
    id "com.gorylenko.gradle-git-properties" version "1.5.2"
}

def props = new Properties()
file("../../../MAP-code/src/P2Protelis/gradle.properties").withInputStream { props.load(it) }
file("../../../MAP-code/src/gradle.common.properties").withInputStream { props.load(it) }
file("../../../gradle.common.properties").withInputStream { props.load(it) }

group = 'com.bbn.map.hifi.apps'
version = props.getProperty("map_hifi_version")

mainClassName = 'com.bbn.map.hifi.apps.filestore.clients.test.Publisher'

description = """Publisher Client for Networked File Store Database"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

spotbugs {
  ignoreFailures = true
  toolVersion = '3.1.12'
}

repositories {
 mavenLocal()
 jcenter()
}

configurations {
    providedRuntime
    all*.exclude group: 'ch.qos.logback'
}

dependencies {
    implementation project(':hifi-utils')

    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: props.getProperty("log4j_version")
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: props.getProperty("log4j_version")
    
    implementation group: 'org.apache.commons', name: 'commons-csv', version: props.getProperty("commons_csv_version")
    
    implementation project(':networked-file-store-server')

    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    
    testImplementation group: 'junit', name: 'junit', version: props.getProperty("junit_version")
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: props.getProperty("hamcrest_version")
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

checkstyle {
    config = resources.text.fromFile('style.xml', 'UTF-8')
    showViolations = true
    ignoreFailures = false
    toolVersion = "8.10"
}

// need to delete the bin directory created by IDEs
clean {
  delete 'bin'
}

task sourceJar(type: Jar) {
  classifier "sources"
  from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }
    
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

gitProperties {
    gitPropertiesDir = "${buildDir}/resources/main/com/bbn/map/hifi/apps/networked_file_store"
}

shadowJar {
    classifier = 'executable'
    
    // for some reason including the Log4j2 dat file causes problems
    // excluding the file causes log4j4 to scan for plugins and everything works 
    exclude 'META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat'
 }
